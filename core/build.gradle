apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
// Android plugin - For publishing to artifactory.
apply from: "$CONFIG.internalConfigDir/publish/android.gradle"

android {
  compileSdkVersion androidCompileSdk

  defaultConfig {
    minSdkVersion androidMinSdk
    targetSdkVersion androidCompileSdk
    versionCode versionCode
    versionName version

    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
  }

  buildTypes {
    release {
      useProguard false
      buildConfigField('String', 'MINI_APP_BASE_URL', "\"${property('MINI_APP_BASE_URL')}\"")
    }
    debug {
      useProguard false
      buildConfigField('String', 'MINI_APP_BASE_URL', "\"${property('MINI_APP_BASE_URL_STG')}\"")
    }
    dev {
      useProguard false
      buildConfigField('String', 'MINI_APP_BASE_URL', "\"${property('MINI_APP_BASE_URL_DEV')}\"")
    }
  }

  sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
    test.java.srcDirs += 'src/test/kotlin'
  }

  compileOptions {
    // Source code java level(Source code of this project).
    sourceCompatibility JavaVersion.VERSION_1_8
    // Target JVM level(JVM on Android devices).
    targetCompatibility JavaVersion.VERSION_1_8
  }

  kotlinOptions {
    jvmTarget = JavaVersion.VERSION_1_8
  }

  testOptions {
    unitTests.returnDefaultValues = true
    unitTests.includeAndroidResources = true
  }

  buildToolsVersion = androidBuildTool
}

dependencies {
  compileOnly "javax.annotation:jsr250-api:$jsr250"

  implementation "androidx.core:core-ktx:$androidx_coreKtx"
  implementation "com.google.dagger:dagger:$dagger"
  implementation "com.jakewharton.timber:timber:$timber"
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

  kapt "com.google.dagger:dagger-compiler:$dagger"

  testImplementation "androidx.test.ext:junit:$androidx_test_ext"
  testImplementation "com.google.truth:truth:$truth"
  testImplementation "org.mockito:mockito-android:$mockito"
  testImplementation "org.mockito:mockito-core:$mockito"
  testImplementation "org.robolectric:robolectric:$robolectric"
}

tasks.whenTaskAdded { task ->
  preBuild.dependsOn 'formatKotlin'
}

publishing {
  publications {
    core(MavenPublication, androidArtifact())
  }
}
